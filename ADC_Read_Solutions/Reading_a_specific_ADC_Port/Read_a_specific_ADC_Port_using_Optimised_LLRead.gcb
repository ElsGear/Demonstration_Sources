'''A program  for GCGB and GCB.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program shows how to create an explicit solution to read the ADC for a specific port on a specific microcontroller.
'''
'''This program is for the 16F15355, read a 10k pot on PortA.2. This uses software serial to simply usage.
'''
'''The program maps the ADC macro LLReadAD to myLLReadAD. Where myLLReadAD has the optimised code for the specific ADC operation.
'''When the macro is completed the program will return the processing the the ADC method used such as ReadAD10
'''
'''@author     EvanV
'''@licence    GPL
'''@version    1.00a
'''@date       7/12/17
'''********************************************************************************

; ----- Configuration
#chip 16F15355, 32


#define Comms_OutPut_Port  PORTC.6    'TxD Pin for serial

'*** Config Software-UART ***
Dir Comms_OutPut_Port Out
#define SendAHigh Set Comms_OutPut_Port ON
#define SendALow  Set Comms_OutPut_Port OFF
InitSer 1, r19200, 1+WaitForStart, 8, 1, None, Normal
Set Comms_OutPut_Port ON

SerPrint 1, " PIC 16f15355"

SerSend 1, 13  ' "Carriage Return"
SerSend 1, 10  ' "Line Feed"


do
  SerPrint 1,readad10(AN2)
  SerSend 1, 13  ' "Carriage Return"
  SerSend 1, 10  ' "Line Feed"
  wait 500 ms
loop

End



#define LLReadAD  myLLReadAD
#define AD_Delay 8 10us

macro myLLReadAD (ADLeftAdjust)

    'specifically set porta.2 as analog
    Set ANSELA.2 On

    'FOSC/16 in ADCON1 selecting the processing time
    SET ADCS2 OFF
    SET ADCS1 ON
    SET ADCS0 OFF

'    SerPrint 1,ADCON1
'    SerSend 1, 9


    'GOnDONE stop; ADON enabled; CHS ANA0;
    ADCON0 = 0x01;

    ;Clear port
    SET CHS0 OFF
    SET CHS1 OFF
    SET CHS2 OFF
    SET CHS3 OFF
    SET CHS4 OFF
    ;Choose port in ADCON0 setting the channel
    IF ADReadPort.0 On Then Set CHS0 On
    IF ADReadPort.1 On Then Set CHS1 On
    IF ADReadPort.2 On Then Set CHS2 On
    If ADReadPort.3 On Then Set CHS3 On
    If ADReadPort.4 On Then Set CHS4 On

'    SerPrint 1,ADCON0
'    SerSend 1, 9

    ;Enable A/D
    SET ADON ON

    ;Acquisition Delay
    Wait AD_Delay

    ;Read A/D
    SET GO ON
    Wait While GO ON

    ;Switch off A/D
    SET ADCON0.ADON OFF

    ;Clear whatever ANSEL variants the chip has
    ANSELA = 0

end macro

