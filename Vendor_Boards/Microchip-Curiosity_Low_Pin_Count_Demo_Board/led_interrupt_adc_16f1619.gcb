'''Program    led_interrupt_adc_16f1619
'''Compiler   gcbasic v0.98.02 2019-05-16 (UNIX)
'''
'''Microchip Curiosity 8/14/20 pin 8 bit demo board
'''o PIC16F1619 (http://microchip.com/pic16f1619)
'''o LEDs D4-D7 on RA5, RA1/PGEC, RA2, RC5
'''o Switch S1 on RC4
'''o Switch S2 on RA3/MCLRE
'''o Potentiometer POT1 on RC0/AN4
'''
'''On startup LEDs D4-D7 light up and extinguish
'''LED D4 flashes
'''Changing POT1 varies speed at which LED D4 flashes
'''Pressing and releasing S1 toggles LED D1 on/off
'''
'''@author  Trevor B Roydhouse
'''@licence Public Domain Unlicense (see https://unlicense.org/)
'''@version 1.0
'''@date    24 August 2019
'''***************************************************************************************

' ---- Configuration

'Microcontroller @ 32MHz
#CHIP 16F1619, 32

'MCLRE set on so that switch S2 can reset microcontroller
#CONFIG MCLRE_ON


'Ensure all variables are explicitly defined
#OPTION Explicit

' ---- Define hardware settings

#DEFINE S1 PORTC.4
Dir S1 In

#DEFINE Led1 PORTA.5
Dir PORTA.5 Out

#DEFINE Led2 PORTA.1
Dir PORTA.1 Out

#DEFINE Led3 PORTA.2
Dir PORTA.2 Out

#DEFINE Led4 PORTC.5
Dir PORTC.5 Out

#DEFINE POT1 AN4
Dir PORTC.0 In

'Enable interrupt for S1 pin (RC4) with positive going edge
'- generates interrupt on S1 release, not S1 press
Set IOCCP4 On

' ---- Constants

'minimum delay in ms
#DEFINE minMsDelay 80

'startup wait in seconds
#DEFINE waitSecs 1

' ---- Variables

Dim ADCvalueWord As Word

' ---- Main body of program

'On S1 pin RC4 interrupt, call PortChanged subroutine
On Interrupt PortChange Call PortChanged

'Enable interrupts
IntOn

'LED init on program startup
Wait waitSecs s
Set LED1 = 1
Set LED2 = 1
Set LED3 = 1
Set LED4 = 1
Wait waitSecs s
Set LED1 = 0
Set LED2 = 0
Set LED3 = 0
Set LED4 = 0

'Program loop
Do Forever
    'Read ADC
    ADCvalueWord = ReadAD10(POT1)
    'preserve minMsDelay delay
    If ADCvalueWord < minMsDelay Then ADCvalueWord = minMsDelay

    'Flash LED
    Set LED1 = 1
    Wait ADCvalueWord ms
    Set LED1 = 0
    Wait ADCvalueWord ms
Loop

' ---- Subroutines

'Interrupt subroutine called when pin RC4 changes
' - note: you would normally need to check which
'   port pin caused this subroutine to be entered
'   but in this program we have only enabled an
'   interrupt for one pin, so we can omit the check
Sub PortChanged
    'Disable interrupts
    IntOff

    'Clear RC4 Interrupt Flag
    Set IOCCF4 = 0

    'Debounce Switch S1 which is high until pressed
    Wait While S1 = 0

    If LED4 = 0 Then
        Set LED4 = 1
    Else
        Set LED4 = 0
    End If

    'Re-enable interrupts
    IntOn
End Sub
