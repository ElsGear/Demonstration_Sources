'''Program    cvd_touch_button_16f1619
'''Compiler   gcbasic v0.98.02 2019-05-16 (UNIX)
'''
'''Microchip Curiosity 8/14/20 pin 8 bit demo board
'''o PIC16F1619 (http://microchip.com/pic16f1619)
'''o LEDs D4-D7 on RA5, RA1/PGEC, RA2, RC5
'''o Switch S1 on RC4
'''o Switch S2 on RA3/MCLRE
'''o Touch Switch S3 on RC1/AN5
'''o Potentiometer POT1 on RC0/AN4
'''
'''An Implementation of Microchip Application Note AN1298
'''- Capacitive Touch Using Only an ADC ("CVD")
'''(http://ww1.microchip.com/downloads/en/AppNotes/01298A.pdf)
'''
'''On startup LEDs D4-D7 turn on and off
'''LED D4 turns on while S3 touch sensor is touched
'''LED D4 turns off when S3 touch sensor is not touched
'''
'''@author  Trevor B Roydhouse
'''@licence Public Domain Unlicense (see https://unlicense.org/)
'''@version 1.0
'''@date    10 September 2019
'''***************************************************************************************

' ---- Configuration

'Microcontroller @ 32MHz
#CHIP 16F1619, 32


'MCLRE set on so that switch S2 can reset microcontroller
#CONFIG MCLRE_ON


'Ensure all variables are explicitly defined
#OPTION Explicit

' ---- Define hardware settings

#DEFINE S1 PORTC.4
Dir S1 In

#DEFINE Led1 PORTA.5
Dir PORTA.5 Out

#DEFINE Led2 PORTA.1
Dir PORTA.1 Out

#DEFINE Led3 PORTA.2
Dir PORTA.2 Out

#DEFINE Led4 PORTC.5
Dir PORTC.5 Out

#DEFINE POT1 AN4
Dir PORTC.0 In

' AN5/RC1 - sensor channel
#DEFINE SensorChannel PORTC.1	
Dir SensorChannel In

' AN6/RC2 - secondary (dummy) channel
#DEFINE SecondaryChannel PORTC.2
Dir SecondaryChannel Out

' ---- Constants

'startup wait in seconds
#DEFINE waitSecs 1

' ---- Variables

Dim ADCvalueWord As Word

' ---- Main body of program

'LED init on program startup
Wait waitSecs s
Set LED1 = 1
Set LED2 = 1
Set LED3 = 1
Set LED4 = 1
Wait waitSecs s
Set LED1 = 0
Set LED2 = 0
Set LED3 = 0
Set LED4 = 0

Set ADCvalueWord = 0

'Program infinite loop
Do Forever

    'AN1298 - Step 1: Set secondary (dummy) channel high (VDD)
    'set as output
    Dir SecondaryChannel Out	
    'set as open drain
    Set ODCONC.2 = 1
    'set as digital
    Set ANSELC.2 = 0
    'set high (VDD)
    Set SecondaryChannel = 1

    'AN1298 - Step 2: Select secondary channel on ADC (charge CHOLD to VDD)
    'ADC channel select -> AN6/RC2 (bits 2-6)
    Set ADCON0 = 0b00011000

    'AN1298 - Step 3: Set sensor pin as output, low (GND) to eliminate any charge
    'Set as output
    Dir SensorChannel Out
    'Set as digital
    Set ANSELC.1 = 0
    'set low (ground)
    Set SensorChannel = 0

    'AN1298 - Step 4: Set sensor pin as input
    'Set as input
    Dir SensorChannel In	
    'Set as analog
    Set ANSELC.1 = 1

    'AN1298 - Step 5: Select sensor channel on ADC (V-div sensor:CHOLD)
    'ADC channel select -> AN5/RC1 (bits 2-6)
    Set ADCON0 = 0b00010101
    'Enable ADC (bit 0)
    'Delay to allow ADC voltage to stabilise
    Wait 10 us 		

    'AN1298 - Step 6: Start ADC conversion
    'Start ADC conversion (set go/done bit 1 = 1)
    Set ADCON0.1 = 1  	
    'Wait for ADC conversion (done bit 1 = 0)
    Wait While ADCON0.1 = 1

    'AN1298 - Step 7: Read ADC result
    ADCvalueWord = ReadAD10(AN5)

    'Action result
    If ADCvalueWord > 5 Then
        'If touching S3 touch button
        LED1 = 1
    Else
        LED1 = 0
    End If

Loop
